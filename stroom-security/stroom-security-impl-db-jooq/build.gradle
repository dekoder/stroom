apply plugin: 'nu.studer.jooq'

ext.moduleName = 'stroom.security.impl.db.jooq'

dependencies {
    implementation libs.jakarta.annotation.api
    implementation libs.jooq

    runtimeOnly libs.mysql.connector.java
    jooqGenerator libs.mysql.connector.java
}


// Configure jooq plugin to generate code
jooq {
    version = versions.jooq
    edition = nu.studer.gradle.jooq.JooqEdition.OSS  // default (can be omitted)

    configurations {
        main {  // name of the jOOQ configuration

            // Disable JOOQ code generation on build
            generateSchemaSourceOnCompilation = false

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'com.mysql.cj.jdbc.Driver'
                    // Parsed from local.yml
                    url = jdbcUrl
                    user = 'stroomuser'
                    password = 'stroompassword1'
                }
                generator {
                    name = 'org.jooq.codegen.JavaGenerator'
                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        // Parsed from local.yml
                        inputSchema = dbSchema
                        // The schema name to use in the genned classes, instead of inputSchema
                        outputSchema = 'stroom'
                        // Add anything you want included in generation below, whitespace ignored
                        // and comments allowed. Each one is a java regex
                        includes = '' +
                                'api_key|' +
                                'stroom_user|' +
                                'stroom_user_archive|' +
                                'stroom_user_group|' +
                                'permission_app|' +
                                'permission_app_id|' +
                                'permission_doc|' +
                                'permission_doc_id|' +
                                'permission_doc_create|' +
                                'permission_doc_type_id'
                        // We don't want to include flyway versioning
                        excludes = 'security_schema_history'
                        // Specify 'version' for use in optimistic concurrency control
                        recordVersionFields = 'version'
                        // Treat some tinyint columns as booleans
                        forcedTypes {
                            forcedType {
                                name = 'BOOLEAN'
                                includeExpression = '' +
                                        '.*\\.api_key\\.enabled|' +
                                        '.*\\.stroom_user\\.is_group|' +
                                        '.*\\.stroom_user\\.enabled|' +
                                        '.*\\.stroom_user_archive\\.is_group'
                                // see https://github.com/jOOQ/jOOQ/issues/9405
                                includeTypes = '(?i:tinyint)(\\(1\\))?'
                            }
                        }
                    }
                    //generate {
                        // TODO: When we upgrade to jooq 3.19 we may want this to stop warnings
                        // relating to the self join in stroom_user_group as we don't use implicit joins
                        // Implicit joind Seem like syntactic sugar that we don't need
                        //implicitJoinPathsToOne = false
                        //implicitJoinPathsToMany = false
                    //}
                    target {
                        packageName = 'stroom.security.impl.db.jooq'
                        directory = 'src/main/java'
                    }
                }
            }
        }
    }
}
